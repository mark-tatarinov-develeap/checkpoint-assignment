node {
    def awsRegion    = 'us-west-2'
    def producerRepo = ''
    def consumerRepo = ''
    def producerTag  = ''
    def consumerTag  = ''

    try {
        stage('Checkout') {
            cleanWs()

            withCredentials([usernamePassword(credentialsId: 'git-creds',
                                              usernameVariable: 'USERNAME',
                                              passwordVariable: 'PAT')]) {
                sh '''
                    set -e
                    echo "Cloning repo with user ${USERNAME}"
                    git clone https://${USERNAME}:${PAT}@github.com/mark-tatarinov-develeap/checkpoint-assignment.git .
                '''
            }
        }

        stage('Compute version tags') {
            def versionText = readFile 'version.json'
            def versionJson = new groovy.json.JsonSlurper().parseText(versionText)

            def PRODUCER_MAJOR = versionJson.producer.major as int
            def PRODUCER_MINOR = versionJson.producer.minor as int

            def CONSUMER_MAJOR = versionJson.consumer.major as int
            def CONSUMER_MINOR = versionJson.consumer.minor as int

            // Use Jenkins build number as PATCH
            def PATCH = env.BUILD_NUMBER as int

            producerTag = "${PRODUCER_MAJOR}.${PRODUCER_MINOR}.${PATCH}"
            consumerTag = "${CONSUMER_MAJOR}.${CONSUMER_MINOR}.${PATCH}"

            echo "Computed producer image tag: ${producerTag}"
            echo "Computed consumer image tag: ${consumerTag}"

            currentBuild.displayName = "#${env.BUILD_NUMBER} - p:${producerTag} c:${consumerTag}"
        }

        stage('Resolve AWS account & repos') {

            withCredentials([[$class: 'AmazonWebServicesCredentialsBinding',
                              credentialsId: 'aws-creds']]) {
                def accountId = sh(
                    script: "aws sts get-caller-identity --query Account --output text --region ${awsRegion}",
                    returnStdout: true
                ).trim()

                echo "Using AWS account: ${accountId}"

                producerRepo = "${accountId}.dkr.ecr.${awsRegion}.amazonaws.com/producer"
                consumerRepo = "${accountId}.dkr.ecr.${awsRegion}.amazonaws.com/consumer"

                echo "Producer repo: ${producerRepo}"
                echo "Consumer repo: ${consumerRepo}"
            }
        }

        stage('Login to ECR') {
            withCredentials([[$class: 'AmazonWebServicesCredentialsBinding',
                              credentialsId: 'aws-creds']]) {
                sh """
                    set -e
                    aws ecr get-login-password --region ${awsRegion} \
                      | docker login --username AWS --password-stdin ${producerRepo.split('/')[0]}
                """
            }
        }

        stage('Build images') {
            echo "Building producer image: ${producerRepo}:${producerTag}"
            sh """
                set -e
                docker build -t ${producerRepo}:${producerTag} ./producer/.
            """

            echo "Building consumer image: ${consumerRepo}:${consumerTag}"
            sh """
                set -e
                docker build -t ${consumerRepo}:${consumerTag} ./consumer/.
            """
        }

        stage('Push images') {
            echo "Pushing producer image: ${producerRepo}:${producerTag}"
            sh "docker push ${producerRepo}:${producerTag}"

            echo "Pushing consumer image: ${consumerRepo}:${consumerTag}"
            sh "docker push ${consumerRepo}:${consumerTag}"
        }

        stage('Summary') {
            echo "Images pushed successfully:"
            echo "  Producer: ${producerRepo}:${producerTag}"
            echo "  Consumer: ${consumerRepo}:${consumerTag}"
        }

    } catch (e) {
        echo "Build failed: ${e}"
        currentBuild.result = 'FAILURE'
        throw e
    } finally {
        cleanWs()
    }
}
