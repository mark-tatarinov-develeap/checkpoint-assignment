node {
    // Adjust as needed
    def awsRegion    = 'us-west-2'
    def producerRepo = ''
    def consumerRepo = ''
    def imageTag     = ''

    try {
        stage('Checkout') {
            cleanWs()

            withCredentials([usernamePassword(credentialsId: 'git-creds',
                                              usernameVariable: 'USERNAME',
                                              passwordVariable: 'PAT')]) {
                sh '''
                    set -e
                    echo "Cloning repo with user ${USERNAME}"
                    git clone https://${USERNAME}:${PAT}@github.com/mark-tatarinov-develeap/checkpoint-assignment.git .
                '''
            }
        }

        stage('Compute semantic version tag') {
            def versionText = readFile 'version.json'
            def versionJson = new groovy.json.JsonSlurper().parseText(versionText)

            def MAJOR = versionJson.major as int
            def MINOR = versionJson.minor as int
            def PATCH = env.BUILD_NUMBER as int 

            imageTag = "${MAJOR}.${MINOR}.${PATCH}"

            echo "Computed semantic image tag from version.json + build number: ${imageTag}"
            currentBuild.displayName = "#${env.BUILD_NUMBER} - v${imageTag}"
}

        stage('Resolve AWS account & repos') {

            withCredentials([[$class: 'AmazonWebServicesCredentialsBinding',
                              credentialsId: 'aws-creds']]) {
                def accountId = sh(
                    script: "aws sts get-caller-identity --query Account --output text --region ${awsRegion}",
                    returnStdout: true
                ).trim()

                echo "Using AWS account: ${accountId}"

                producerRepo = "${accountId}.dkr.ecr.${awsRegion}.amazonaws.com/producer"
                consumerRepo = "${accountId}.dkr.ecr.${awsRegion}.amazonaws.com/consumer"

                echo "Producer repo: ${producerRepo}"
                echo "Consumer repo: ${consumerRepo}"
            }
        }

        stage('Login to ECR') {
            withCredentials([[$class: 'AmazonWebServicesCredentialsBinding',
                              credentialsId: 'aws-creds']]) {
                sh """
                    set -e
                    aws ecr get-login-password --region ${awsRegion} \
                      | docker login --username AWS --password-stdin ${producerRepo.split('/')[0]}
                """
            }
        }

        stage('Build images') {
            echo "Building producer image: ${producerRepo}:${imageTag}"
            sh """
                set -e
                docker build -t ${producerRepo}:${imageTag} ./producer/.
            """

            echo "Building consumer image: ${consumerRepo}:${imageTag}"
            sh """
                set -e
                docker build -t ${consumerRepo}:${imageTag} ./consumer/.
            """
        }

        stage('Push images') {
            echo "Pushing producer image: ${producerRepo}:${imageTag}"
            sh "docker push ${producerRepo}:${imageTag}"

            echo "Pushing consumer image: ${consumerRepo}:${imageTag}"
            sh "docker push ${consumerRepo}:${imageTag}"
        }

        stage('Summary') {
            echo "Images pushed successfully:"
            echo "  Producer: ${producerRepo}:${imageTag}"
            echo "  Consumer: ${consumerRepo}:${imageTag}"
        }

    } catch (e) {
        echo "Build failed: ${e}"
        currentBuild.result = 'FAILURE'
        throw e
    } finally {
        cleanWs()
    }
}
